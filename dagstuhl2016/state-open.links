# helper functions for invoking Get and Put operations
sig get : () {Get:s|_}-> s
fun get() {do Get}

sig put : (s) {Put:(s) {}-> ()|_}-> ()
fun put(s) {do Put(s)}

# bit toggling example
sig toggle : () {Get:Bool, Put:(Bool) {}-> ()|_}-> Bool
fun toggle() {
  var x = get();
  put(not(x));
  x
}

sig evalState :
          (Comp ({Get:s,  Put:(s) {}-> ()|e}, a)) ->
    (s) -> Comp ({Get{_}, Put{_}         |e}, a)
fun evalState(m)(s)() {
  (open handle(m) {
    case Return(x) -> fun (_) {x}
    case Get(k)    -> fun (s) {k(s)(s)}
    case Put(t, k) -> fun (s) {k(())(t)}
  })(s)
}

sig logState :
          (Comp ({Get:s,  Put:(s) {}-> ()|e}, a)) ->
    (s) -> Comp ({Get{_}, Put{_}         |e}, (a, [s]))
fun logState(m)(s)() {
  (open handle(m) {
    case Return(x) -> fun (s) {(x, [s])}
    case Get(k)    -> fun (s) {k(s)(s)}
    case Put(t, k) -> fun (s) {var (x, ss) = k(())(t);
                               (x, s::ss)}
  })(s)
}

var x1 = evalState(toggle)(true)();
var x2 = logState(toggle)(true)();

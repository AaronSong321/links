var db = database "test" "postgresql" ":5432:test:foobar";
var players_table = table "players" with (id : Int, team : String, name : String, pos : String, eff : Int) tablekeys [["id"]] from db;

fun centers(p) { p.pos == "C" }
fun forwards_guards(p) { p.pos == "F" || p.pos == "G" }
fun efficient_players(pos, eff)(p) { p.pos == pos && p.eff > eff }
fun center(p) { Center(p.name, p.team, p.eff) }
fun other(p) { Other(p.name, p.team) }
fun max_eff(ps) { max(for (p <-- ps) [p.eff]) }

var l = [(pred = centers, proj = center, group = Just(fun (p) { p.team })), 
	(pred = forwards_guards, proj = other, group = Nothing), 
	(pred = efficient_players("C", 32), proj = center, group = Nothing)];

fun apply_queries(qs, t) {
	query {
		for (q <- qs) {
			var rs = for (r <-- t) where (q.pred(r)) [r];
			var grouped = 
				switch (q.group) {
					case Just(g) -> Group(groupByBase(g, rs))
					case Nothing -> Flat(rs)
				};
			[switch (grouped) {
				case Group(gs) -> Group(for (g <- gs) [(g.1, for (r <- g.2) [q.proj(r)])])
				case Flat(rs) -> Flat(for (r <- rs) [q.proj(r)])
			}]
		}
	}
}

apply_queries(l, players_table)

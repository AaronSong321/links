typename Calc = mu a.[&|Add:?Int.?Int.!Int.a, Mul:?Int.?Int.!Int.a, Stop:End|&];

sig calc : (Calc) ~> ()
fun calc(s) {
  offer (s) {
    case Add(s) ->
      var (x,s) = grab(s);
      var (y,s) = grab(s);
      var s = give(x+y,s);
      calc(s)
    case Mul(s) ->
      var (x,s) = grab(s);
      var (y,s) = grab(s);
      var s = give(x*y,s);
      calc(s)
    case Stop(s) ->
      ()
  }
}

sig user : (~Calc) ~> Int
fun user(s) {
  var s = select Mul s;
  var (v, s) = grab(give(6,give(7,s)));
  var s = select Add s;
  var (v, s) = grab(give(v,give(v,s)));
  var _ = select Stop s;
  v
}

fun main() {
  var a = new ();
  var _ = spawn { calc(accept(a)) };
  user(request(a))
}

main()

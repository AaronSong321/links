typename Shop  = mu a.[&|Add:?String.a, Checkout:?Int.?String.End|&];


sig waitForClient : (Shop) ~> ()
fun waitForClient(s) client {
  offer(s) {
    case Add(s) ->
      var (item, s) = grab(s);
      appendChildren(stringToXml("Book name received: " ^^ item), getNodeById("items"));
      waitForClient(s)

    case Checkout(s) ->
      var (card, s)    = grab(s);
      appendChildren(stringToXml(", card number received: " ^^ intToString(card)), getNodeById("items"));
      var (address, s) = grab(s);
      appendChildren(stringToXml(", address received: " ^^ address), getNodeById("items"));
      ()
  }
}

sig orderBooks : (~Shop) ~> ()
fun orderBooks(c) client {
  var c = give("Alice in Wonderland", select Add c);
  var _ = give("Summerhall Square", give(8753, select Checkout c));
  ()
}

sig main : () ~> ()
fun main() client {
  var accessPoint = new();
  var _ = spawn { waitForClient(accept(accessPoint))};
  orderBooks(request(accessPoint))
}


page
  <html>
  <head><title>From Academic Papers</title></head>
  <body>
  <form l:onsubmit="{main()}">
  <h3>Example 1 from "Linear type theory for asynchronous session types":</h3>
  <button type="submit">Start communication</button>
  </form>
  <p id="items"></p>
  </body>
  </html>

typename Domain = String;
typename Address = String;
typename Accept = String;
typename Message = String;

typename SMTPServer = [&|EHLO:?Domain.!Accept.
					    [&|MAIL:?Address.!Accept.
					      [&|RCPT:?Address.!Accept.
					        [&|DATA:?Message.!Accept.
					          [&|QUIT:EndBang|&]|&]|&]|&]|&];


#typename SMTPClient = [+|EHLO:!Domain.?Accept.
#                        [+|MAIL:!Address.?Accept.
#                          [+|RCPT:!Address.?Accept.
#                            [+|DATA:!Message.?Accept.
#                              [+|QUIT:EndBang|+]|+]|+]|+]|+];





sig mailServer : (SMTPServer) ~> EndBang
fun mailServer(s) {
	offer (s) {
   	case EHLO(s) ->
     		var (x, s) = receive(s);
     		print("S: received domain: " ^^ x);
     		var s = send("C: 250 OK", s);
    		offer(s) {
    			case MAIL(s) ->
     				var (x, s) = receive(s);
     				print("S: received address from: " ^^ x);
     				var s = send("C: 250 OK", s);
    				
    				offer(s) {
    					case RCPT(s) ->
     							var (x, s) = receive(s);
     							print("S: received address to: " ^^ x);
     							var s = send("C: 250 OK", s);
    							
    							offer(s) {
    								case DATA(s) ->
     									var (x, s) = receive(s);
     									print("S: received message: " ^^ x);
     									var s = send("C: 250 OK", s);
    									
    								    offer(s) {
                                            case QUIT(s) -> s
    									}
    							}
    				}
    		}
     }
     
}

sig mailClient : (~SMTPServer) ~> ()
fun mailClient(c) {
	 var c = send("mydomain.com", select EHLO c);
	 var (x, c) = receive(c);
	 print(x);

 	 var c = send("starlight@domain.com", select MAIL c);
 	 var (x, c) = receive(c);
	 print(x);

	 var c = send("friend@galaxy.com", select RCPT c);
 	 var (x, c) = receive(c);
	 print(x);

	 var c = send("Hello to bravest warriors!", select DATA c);
 	 var (x, c) = receive(c);
	 print(x);

	 var c = select QUIT c;
     wait(c);
  	 ()
}


fun main() {
    mailClient(fork(mailServer))
}

main()
var errMsg = "";
var cookieName = "linkside";

typename User = (name:String, password:String);

# Database to access
var users = table "users" with (
    id : Int,
    email: String,
    password: String
) from (database "ide");

# Count the number of elements in a list
fun numElems(lst) 
{
  switch(lst)
  {
    case [] -> 0
    case x::xs -> 1 + numElems(xs)
  }
}

# Login formlet
var loginForm =
  formlet
    <#>
      <div id="loginform">
        <label>e-mail address</label>
         { input -> email }
        <label>pass</label>
         { inputPassword -> pass}
      {submitButton("log in") -> change}
      </div>
    </#>
  yields
    (name=email,password=pass);

# Login function
fun login (data) server
{
  var uid =
  for (var user <-- users)
   where ((user.email == data.name) && (user.password == (md5(data.password))))
    [user.id];

  if (numElems (uid) == 1)
  {
    debug(md5(data.password));
    var id = md5(floatToString(getRandom() *. (intToFloat(hd(uid)))));
    #delete (var session <-- sessionsTable)
    # where (session.uid == hd(uid));
    #insert sessionsTable values [(id=id,uid=hd(uid),creation_time=0,update_time=0,valid=0)];
    setCookie(cookieName, id);
    freshResource();
    redirect("index.links");
    <#></#>
  }
  else
  {
    freshResource();
    <#>Error!</#>
  }
}

fun displayForm () server
{
  render(loginForm, login)
}

fun getMenu ()
{
  <table cellpadding="6" cellspacing="0">
   <tr>
    <td><a href="main.links">main</a></td>
    <td><a href="">browse</a></td>
    <td><a href="">random</a></td>
   </tr>
  </table>
}

fun content ()
{
  <div id="content">
  {
    displayForm()
  }
  </div>
}

<html>
  <head>
    <title>Login</title>
    <link rel="stylesheet" type="text/css" href="proj.css" />
  </head>
  <body>
  {
    content()
  }
  </body>
</html>

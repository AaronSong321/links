# Databases to access
var users = table "users" with (
    id : Int,
    email: String,
    password: String
) from (database "ide");

var sessionsTable = table "sessions" with (
    id : String,
    uid : Int,
    creation_time : Int,
    update_time : Int
) from (database "ide");

# Count the number of elements in a list
fun numElems(lst) server
{
  switch(lst)
  {
    case [] -> 0
    case x::xs -> 1 + numElems(xs)
  }
}

var cookieName = "linkside";
var fileLoc = "/home/tom/links/html/proj/";
var fileDir = "test/";

# Login formlet
var loginForm =
  formlet
    <#>
      <div class="box">
          <div class="topbar">
            <span class="caption-text" unselectable="on">Login</span>
          </div>
          <div class="container-box">
            <span class="proj-items padded">
              <p>Username:<br />
                { input -> email }
              </p>
              <p>Password: <br />
                { inputPassword -> pass }
              </p>
          </span>
        </div>
        <div class="bottombar">
          {submitButton("Log in") -> change}
        </div>
      </div>
    </#>
  yields
    (name=email,password=md5(pass));

# Login function
fun login (data) server
{
  var foundIds =
  for (var user <-- users)
   where ((user.email == data.name) && (user.password == (data.password)))
    [user.id];

  if (numElems (foundIds) == 1)
  {
    var uid = hd(foundIds);

    # Create a hash-value to use for the cookie. This is
    # also used to identify the session and the user.
    var id = md5(floatToString(random() *. (intToFloat(uid))));
  
    # Clean up any old sessions
    delete (var session <-- sessionsTable)
     where (session.uid == uid);
        
    # Create a new one
    insert sessionsTable values [(id = id, uid = uid, creation_time = 0, update_time = 0)];

    # Create user dir
    switch (mkdir(fileLoc ++ fileDir ++ id))
    {
      case Error(err) -> debug("Error: " ++ err)
      case Ok() -> ()
    };

    # Set cookie and redirect
    setCookie(cookieName, id);
    redirect("main.links");
    page
      <#></#>
  }
  else
  {
    main(Just(true))
  }
}

#fun displayForm() server
#{
#  render(loginForm, login)
#}

fun main(flag) {
  page
  #var loggedIn =
  #        switch (flag) {
  #          case Nothing -> isLoggedIn()
  #          case Just(loggedIn) -> loggedIn
  #        };   
  <html>
    <head>
      <title>ug4proj: Links</title>
    </head>
    <link rel="stylesheet" type="text/css" href="proj.css" />
    <body>
      <div id="main" style="margin-left: auto; margin-right: auto; width: 375px;">	
        <img src="images/lode-logo.png" style="display: block;"/>
        <div style="margin-left: auto; margin-right: auto; width: 230px;">
        {
          loginForm => login
        }
        </div>
      </div>
    </body>
  </html>
}

main(Nothing)

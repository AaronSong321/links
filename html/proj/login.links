# Databases to access
var users = table "users" with (
    id : Int,
    email: String,
    password: String
) from (database "ide");

var sessionsTable = table "sessions" with (
    id : String,
    uid : Int,
    creation_time : Int,
    update_time : Int
) from (database "ide");

var cookieName = "linkside";
var fileLoc = "/home/tom/links/html/proj/";
var fileDir = "test/";

# Login formlet
var loginForm server =
  formlet
    <#>
      <div>
        <div class="container-box" style="margin-top: 2px; height: 80px;">
          <span class="popup-items padded">
              <p>Username:<br />
                { input -> email }
              </p>
              <p>Password: <br />
                { inputPassword -> pass }
              </p>
          </span>
        </div>
        <div class="bottombar">
          {submitButton("Log in") -> change}
        </div>
      </div>
    </#>
  yields
    (name=email,password=md5(pass));

# Login function
fun login (data) server
{
  var foundIds =
  for (var user <-- users)
   where ((user.email == data.name) && (user.password == (data.password)))
    [user.id];

  if (length(foundIds) == 1)
  {
    var uid = hd(foundIds);

    # Create a hash-value to use for the cookie. This is
    # also used to identify the session and the user.
    var id = md5(floatToString(random() *. (intToFloat(uid))));
  
    # Clean up any old sessions
    delete (var session <-- sessionsTable)
     where (session.uid == uid);
        
    # Create a new one
    insert sessionsTable values [(id = id, uid = uid, creation_time = 0, update_time = 0)];

    # Create user dir
    switch (mkdir(fileLoc ++ fileDir ++ id))
    {
      case Error(err) -> debug("Error: " ++ err)
      case Ok() -> ()
    };

    # Set cookie and redirect
    setCookie(cookieName, id);
    redirect("main.links");
    page
      <#></#>
  }
  else
  {
    main(Just(true))
  }
}

fun main(flag) {
  page
  #var loggedIn =
  #        switch (flag) {
  #          case Nothing -> isLoggedIn()
  #          case Just(loggedIn) -> loggedIn
  #        };   
  <html>
    <head>
      <title>LODE - An online development environment for Links in Links</title>
    </head>
    <link rel="stylesheet" type="text/css" href="proj.css" />
    <body>
      <div id="main" style="margin-left: auto; margin-right: auto; width: 375px; font-family: Tahoma, Verdana; font-size: 12px;">	
        <img src="images/lode-logo.png" style="display: block; margin-bottom: 6px;"/>
        <span style="margin-left: 2px">
        Welcome to LODE, an online development environment for testing and deploying projects in the <a href="http://groups.inf.ed.ac.uk/links">Links</a> programming language. LODE was developed in Links using a modified version of the JavaScript syntax highlighter <a href="http://marijn.haverbeke.nl/codemirror/index.html">CodeMirror</a>.<br /><br />
       In order to access the editor, you need to log in:
          <div style="width: 230px;">
          {
            loginForm => login
          }
          </div>
        </span>
      </div>
    </body>
  </html>
}

main(Nothing)

sig count : Comp({Get:Int,Put:(Int) {}-> ()|_}, Int)
fun count() client {
  var n = do Get;
  if (n == 0) n
  else { do Put(n-1); count() }
}

sig evalState : (Comp({Get:s,Put:(s) {}-> () |e}, a), s) {Get{_},Put{_} |e}~> a
fun evalState(m,st0) {
  (handle(m) {
    case Return(x)      -> fun(_) { x }
    case Get(resume)    -> fun(resume)(st) { resume(st)(st) }(resume)
    case Put(st,resume) -> fun(st,resume)(_)  { resume(())(st) }(st,resume)
  })(st0)
}

fun b(n) client {
  var t0 = clientTime();
  var r = evalState(count,n);
  var t1 = clientTime();
  ignore $ domSetPropertyFromRef(getNodeById("result"), "value", intToString(r));
  ignore $ domSetPropertyFromRef(getNodeById("elapsed"), "value", intToString(t1 - t0));
  domRemoveAttributeFromRef(getNodeById("run"), "disabled")
}

fun proc() client {
  var n = recv();
  b(n);
  proc()
}

fun start(worker, inputSize) client {
  var n = stringToInt(inputSize);
  ignore $ domSetPropertyFromRef(getNodeById("run"), "disabled", "disabled");
  ignore $ domSetPropertyFromRef(getNodeById("result"), "value", "");
  ignore $ domSetPropertyFromRef(getNodeById("elapsed"), "value", "");
  worker ! n
}

# Page
fun mainPage(_) {
  var p = spawnClient { proc() };
  page
    <html>
      <body>
        <form l:onsubmit="{start(p, inputSize)}">
          <table border="0" cellspacing="5">
            <tr>
              <td>Problem size:</td>
              <td><input type="text" l:name="inputSize" value="100000" /></td>
            </tr>
            <tr>
              <td>Result:</td>
              <td><input type="text" id="result" readonly="readonly" value="" /></td>
            </tr>
            <tr>
              <td>Elapsed:</td>
              <td><input type="text" id="elapsed" readonly="readonly" value="" /></td>
            </tr>
            <tr>
             <td colspan="2"><input type="submit" id="run" value="Run" /></td>
            </tr>
          </table>
        </form>
      </body>
    </html>
}


fun main() {
  addRoute("/", mainPage);
  servePages()
}

main()
